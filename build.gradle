buildscript {

	ext {
		springBootVersion = '1.5.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id "com.moowork.node" version "1.1.1"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'


jar {
	baseName = 'designer-app'
	version = '0.0.1-SNAPSHOT'
	archiveName 'designer-app.jar'
}

sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	compile('com.vaadin:vaadin-spring-boot-starter')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
	imports {
		mavenBom "com.vaadin:vaadin-bom:8.0.0"
	}
}

node {
    npmWorkDir = file("${project.buildDir}/electron")
	nodeModulesDir = file("${project.buildDir}/electron")
}

task copyForElectron(type: Copy, dependsOn: ':build') {
	from jar.archivePath
	from "src/electron"
	into "build/electron/"
}

task packageElectron(type: NpmTask) {
	args = ['run-script', 'package']
}

task electron(dependsOn: [':copyForElectron', ':npmInstall', ':packageElectron'])
